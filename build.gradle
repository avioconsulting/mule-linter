
plugins {
    id 'base'
    id 'net.thauvin.erik.gradle.semver' version '1.0.4'
    id 'org.jreleaser' version '1.4.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

jreleaser {
    configFile = 'jreleaser.yml'
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'net.thauvin.erik.gradle.semver'

    group 'com.avioconsulting.mule'

    semver {
        properties = parent.getProject().getProjectDir().toPath().resolve("version.properties").toString()
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
        withSourcesJar()
    }

    dependencies {
        implementation (group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.8', ext: 'pom'){
            transitive= true
        }
    }

    groovydoc {
        doLast {
            if(project.name == 'mule-linter-core') {
                copy {
                    from 'build/docs/groovydoc'
                    into '../docs/groovydoc'
                }
            }
        }
    }
    task groovydocJar(type: Jar, dependsOn: groovydoc ) {
        classifier 'javadoc' // must use javadoc classifier to be able to deploy to Sonatype
        from groovydoc.destinationDir
    }
    jar.dependsOn(groovydocJar)


    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact(groovydocJar) {
                    classifier = 'javadoc'
                }
                pom {
                    name = project.name
                    description = project.name
                    url = 'https://github.com/avioconsulting/mule-linter'
                    inceptionYear = '2020'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://spdx.org/licenses/MIT.html'
                        }
                    }
                    developers {
                        developer {
                            id = 'adesjardin'
                            name = 'Adam Desjardin'
                        }
                        developer {
                            id = 'kkingavio'
                            name = 'Kevin King'
                        }
                        developer {
                            id = 'manikmagar'
                            name = 'Manik Magar'
                        }
                        developer {
                            id = 'MrMcCartney'
                            name = 'Reed McCartney'
                        }
                        developer {
                            id = 'vanessacobis'
                            name = 'Vanessa Cobis'
                        }

                    }
                    scm {
                        connection = 'scm:git:https://github.com/avioconsulting/mule-linter.git'
                        developerConnection = 'scm:git:ssh://github.com/avioconsulting/mule-linter.git'
                        url = 'http://github.com/avioconsulting/mule-linter'
                    }
                }
            }
        }
//        repositories {
//            maven {
//                url = layout.getBuildDirectory().dir("staging-deploy")
//            }
//        }
    }

    signing {
        sign publishing.publications.maven
    }
}
